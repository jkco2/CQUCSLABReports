\appendix
\section{Datapath代码}

\begin{lstlisting}[language=Verilog,frame=single]
module d_cache (
    input wire clk, rst,
    //mips core
    input         cpu_data_req     ,
    input         cpu_data_wr      ,
    input  [1 :0] cpu_data_size    ,
    input  [31:0] cpu_data_addr    ,
    input  [31:0] cpu_data_wdata   ,
    output [31:0] cpu_data_rdata   ,
    output        cpu_data_addr_ok ,
    output        cpu_data_data_ok ,

    //axi interface
    output         cache_data_req     ,
    output         cache_data_wr      ,
    output  [1 :0] cache_data_size    ,
    output  [31:0] cache_data_addr    ,
    output  [31:0] cache_data_wdata   ,
    input   [31:0] cache_data_rdata   ,
    input          cache_data_addr_ok ,
    input          cache_data_data_ok 
);
    //Cache配置
    parameter  INDEX_WIDTH  = 10, OFFSET_WIDTH = 2;
    localparam TAG_WIDTH    = 32 - INDEX_WIDTH - OFFSET_WIDTH;
    localparam CACHE_DEEPTH = 1 << INDEX_WIDTH;
    
    //Cache存储单元
    reg                 cache_valid [CACHE_DEEPTH - 1 : 0];
    reg [TAG_WIDTH-1:0] cache_tag   [CACHE_DEEPTH - 1 : 0];
    reg [31:0]          cache_block [CACHE_DEEPTH - 1 : 0];
    reg                 cache_dirty [CACHE_DEEPTH - 1 : 0];

    //访问地址分解
    wire [OFFSET_WIDTH-1:0] offset;
    wire [INDEX_WIDTH-1:0] index;
    wire [TAG_WIDTH-1:0] tag;
    
    assign offset = cpu_data_addr[OFFSET_WIDTH - 1 : 0];
    assign index = cpu_data_addr[INDEX_WIDTH + OFFSET_WIDTH - 1 : OFFSET_WIDTH];
    assign tag = cpu_data_addr[31 : INDEX_WIDTH + OFFSET_WIDTH];

    //访问Cache line
    wire c_valid;
    wire [TAG_WIDTH-1:0] c_tag;
    wire [31:0] c_block;
    wire c_dirty;

    assign c_valid = cache_valid[index];
    assign c_tag   = cache_tag  [index];
    assign c_block = cache_block[index];
    assign c_dirty = cache_dirty[index];

    //判断是否命中
    wire hit, miss;
    assign hit = c_valid & (c_tag == tag);
    assign miss = ~hit;

    //读或写
    wire read, write;
    assign write = cpu_data_wr;
    assign read = ~write;

    //FSM
    parameter IDLE = 2'b00, RM = 2'b01, WM = 2'b11;
    reg [1:0] state;
    always @(posedge clk) begin
        if(rst) begin
            state <= IDLE;
        end
        else begin
            case(state)
                IDLE:   state <= cpu_data_req & hit                  ? IDLE :
                                 cpu_data_req & miss & ~c_dirty      ?   RM :
                                 cpu_data_req & miss & c_dirty       ?   WM : IDLE;//Writeback和WriteAllocate策略下的状态机
                RM:     state <= cache_data_data_ok ? IDLE : RM;//由于块只有1个字可以不用回填
                WM:     state <= cache_data_data_ok ? RM : WM;
                
                default: state <= IDLE;
            endcase
        end
    end

    //读内存
    wire read_req;
    reg addr_rcv;
    wire read_finish;
    
    always @(posedge clk) begin
        addr_rcv <= rst ? 1'b0 :
                    read_req & cache_data_addr_ok ? 1'b1 ://AXI接收地址成功
                    read_finish ? 1'b0 : addr_rcv;//结束
    end
    
    assign read_req = (state == RM);
    assign read_finish = read_req & cache_data_data_ok;

    //写内存
    wire write_req;
    reg waddr_rcv;
    wire write_finish;
    
    always @(posedge clk) begin
        waddr_rcv <= rst ? 1'b0 :
                     write_req &  & cache_data_addr_ok ? 1'b1 :
                     write_finish ? 1'b0 : waddr_rcv;
    end
    
    assign write_req = (state == WM);
    assign write_finish = write_req & cache_data_data_ok;

    //output to mips core
    assign cpu_data_rdata   = hit ? c_block : cache_data_rdata;//hit将Cache中数据发送给CPU，miss则将主存返回的数据发送给CPU
    assign cpu_data_addr_ok = (cpu_data_req & hit) | (cache_data_req & cache_data_addr_ok);
    assign cpu_data_data_ok = (cpu_data_req & hit) | (read_finish);

    //output to axi interface
    assign cache_data_req   = (read_req & ~addr_rcv) | (write_req & ~waddr_rcv);
    assign cache_data_wr    = write_req;  // 只有在写回状态才是写操作
    assign cache_data_size  = cpu_data_size;
    assign cache_data_addr  = write_req ? {c_tag, index, offset} : cpu_data_addr;//写回脏数据，读取新数据
    assign cache_data_wdata = write_req ? c_block : cpu_data_wdata;  // 写回时发送脏数据

    //保存地址中的tag, index，防止地址发生改变
    reg [TAG_WIDTH-1:0] tag_save;
    reg [INDEX_WIDTH-1:0] index_save;
    always @(posedge clk) begin
        tag_save   <= rst ? 0 :
                      cpu_data_req ? tag : tag_save;
        index_save <= rst ? 0 :
                      cpu_data_req ? index : index_save;
    end
    
    // 缺失flag，1说明写缺失未处理，缺失写回后进入RM状态进行写分配，如何判断是写缺失还是读缺失
    reg write_miss_pending;
    always @(posedge clk) begin
        if(rst) begin
            write_miss_pending <= 1'b0;
        end
        else if(cpu_data_req & write & miss) begin
            write_miss_pending <= 1'b1;
        end
        else if(read_finish & write_miss_pending) begin
            write_miss_pending <= 1'b0;
        end
    end
    
    //写掩码生成
    wire [31:0] write_cache_data;
    reg [3:0] write_mask;
    
    always @(posedge clk) begin
        if(rst)begin
            write_mask <= 0;
        end
        else if(cpu_data_req & write & miss) begin
            write_mask <= cpu_data_size==2'b00 ?
                            (cpu_data_addr[1] ? (cpu_data_addr[0] ? 4'b1000 : 4'b0100):
                                                (cpu_data_addr[0] ? 4'b0010 : 4'b0001)) :
                            (cpu_data_size==2'b01 ? (cpu_data_addr[1] ? 4'b1100 : 4'b0011) : 4'b1111);
        end
    end 

    assign write_cache_data = cache_data_rdata & ~{{8{write_mask[3]}}, {8{write_mask[2]}}, {8{write_mask[1]}}, {8{write_mask[0]}}} | 
                              cpu_data_wdata & {{8{write_mask[3]}}, {8{write_mask[2]}}, {8{write_mask[1]}}, {8{write_mask[0]}}};

    //写入Cache
    integer t;
    always @(posedge clk) begin
        if(rst) begin
            for(t=0; t<CACHE_DEEPTH; t=t+1) begin
                cache_valid[t] <= 0;
                cache_dirty[t] <= 0;
            end
        end
        else begin
            if(read_finish) begin//写缺失或读缺失
                cache_valid[index_save] <= 1'b1;
                cache_tag[index_save] <= tag_save;
                cache_block[index_save] <= write_miss_pending ? write_cache_data : cache_data_rdata;//写缺失用CPU要写的数据覆写内存读取出的数据并写入到Cache中，读缺失则直接用内存读取出的数据写入到Cache中
                cache_dirty[index_save] <= write_miss_pending ? 1'b1 : 1'b0;
            end
            else if(cpu_data_req & write & hit) begin
                // 写命中，直接更新缓存并设置脏位
                cache_block[index] <= write_cache_data;
                cache_dirty[index] <= 1'b1;
            end
        end
    end
endmodule
\end{lstlisting}